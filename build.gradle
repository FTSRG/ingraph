plugins {
  id 'com.jfrog.bintray' version '1.7.1' apply false
}

allprojects {
  group = 'ingraph'
  version = '0.2.0-SNAPSHOT'
}

ext {
  commonsIoVersion = '2.5'
  commonsMathVersion = '3.6.1'
  emfVersion = '2.10.1'
  guavaVersion = '15.0' // do not upgrade to 21.0 as VIATRA currently uses the ToStringHelper class (https://google.github.io/guava/releases/20.0/api/docs/com/google/common/base/Objects.ToStringHelper.html)
  jmhVersion = '1.17.5'
  junitVersion = '4.12'
  log4jVersion = '1.2.17'
  neo4jDriverVersion = '1.2.1'
  pegdownVersion = '1.6.0'
  scalaShortVersion = '2.11'
  scalaTestVersion = '3.0.1'
  scalaVersion = '2.11.8'
  slizaaVersion = '1.0.0-SNAPSHOT'
  viatraMinorVersion = '.0'
  viatraDseVersion = '0.16' + viatraMinorVersion
  viatraVersion = '1.6' + viatraMinorVersion
  xtendVersion = '2.10.0'
  xtextVersion = xtendVersion
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'scala'
  apply plugin: 'eclipse'
  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'maven-publish'

  jar {
    from sourceSets.main.allJava
    from sourceSets.main.allScala
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    jcenter()
    maven { url 'https://repo.eclipse.org/content/groups/acceleo' }
    maven { url 'https://repo.eclipse.org/content/groups/viatra2' }
    maven { url 'https://clojars.org/repo' }
    maven { url 'https://szdavid92.github.io/ingraph-deps' }
  }

  // disable warnings for Scala projects
  tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = ["-nowarn"]
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
      }
    }
  }

  dependencies {
    compile group: 'org.scala-lang', name: 'scala-library', version: scalaVersion
    testCompile group: 'org.scalatest', name: "scalatest_$scalaShortVersion", version: scalaTestVersion
    testRuntime group: 'org.pegdown', name: 'pegdown', version: pegdownVersion
  }

  configurations.all { exclude group: 'asm' }

  buildscript {
    repositories {
      jcenter()
    }
  }
}
