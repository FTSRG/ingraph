allprojects {
  group = 'ingraph'
  version = '0.3.0-SNAPSHOT'
}

ext {
  commonsExecVersion = '1.3'
  commonsIoVersion = '2.5'
  commonsMathVersion = '3.6.1'
  guavaVersion = '15.0' // do not upgrade to 21.0 as VIATRA currently uses the ToStringHelper class (https://google.github.io/guava/releases/20.0/api/docs/com/google/common/base/Objects.ToStringHelper.html)
  jmhVersion = '1.17.5'
  junitVersion = '4.12'
  log4jVersion = '1.2.17'
  neo4jDriverVersion = '1.2.1'
  pegdownVersion = '1.6.0'
  scalaShortVersion = '2.11'
  scalaTestVersion = '3.0.1'
  scalaVersion = '2.11.8'
  sparkVersion = '2.2.0'
  slizaaVersion = '1.0.0-SNAPSHOT'
  xtextVersion = '2.10.0'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'scala'

  jar {
    from sourceSets.main.allJava
    from sourceSets.main.allScala
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    jcenter()
    maven { url 'https://szdavid92.github.io/ingraph-deps' }
  }

  // disable warnings for Scala projects
  tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = ["-nowarn"]
  }

  dependencies {
    compile group: 'org.scala-lang', name: 'scala-library', version: scalaVersion
    testCompile group: 'org.scalatest', name: "scalatest_$scalaShortVersion", version: scalaTestVersion
    testRuntime group: 'org.pegdown', name: 'pegdown', version: pegdownVersion
  }

  configurations.all { exclude group: 'asm' }

  buildscript {
    repositories {
      jcenter()
    }
  }
}
